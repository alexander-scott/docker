default:
  @just --list

docker_image := "pre-commit"

build_docker_with_buildkit platform="linux/amd64":
    #! /bin/bash
    platform_short=$(echo {{platform}} | cut -d '/' -f2)
    docker buildx build --platform {{platform}} -t {{docker_image}}  --output "type=oci,dest={{docker_image}}_${platform_short}.tar" docker/. && gzip {{docker_image}}_${platform_short}.tar

build_docker:
    #! /bin/bash
    docker build -t {{docker_image}} docker/.


build_docker_amd64:
    #! /bin/bash
    docker buildx build --load --platform linux/amd64 -t {{docker_image}} docker/.

# Install docker buildx and other goodies for multi arch deployment.
setup:
    #! /bin/bash
    sudo apt update
    sudo apt-get install -y binfmt-support qemu-user-static
    sudo apt-get install -y docker.io
    sudo usermod -aG docker $USER

    sudo apt-get install -y jq
    mkdir -p ~/.docker/cli-plugins
    BUILDX_URL=$(curl https://api.github.com/repos/docker/buildx/releases/latest |  jq  '.assets[].browser_download_url' | grep linux-arm64)
    wget $BUILDX_URL -O ~/.docker/cli-plugins/docker-build
    chmod +x ~/.docker/cli-plugins/docker-buildx

    docker buildx create --use --name mbuilder
    docker buildx inspect --bootstrap

test: build_docker
    #! /bin/bash
    function err() {
        echo -e "\e[1;31m${@}\e[0m" >&2
        exit 1
    }
    git config --global user.email "foo@bar.bla"
    git config --global user.name "Foo Bar"
    TEMP_FOLDER=$(mktemp -d)
    cp Justfile .pre-commit-config.yaml $TEMP_FOLDER
    pushd $TEMP_FOLDER
    git init .
    just install_hooks
    echo "print   (  'Hello World'  )   " > foo.py
    git add foo.py
    git commit -m "Commit demo" || true
    git add foo.py
    git commit -m "Add Encoding" || true
    git add foo.py
    git commit -m "Apply black"
    git add foo.py
    git commit -m "Final form"
    grep 'coding: utf-8' foo.py > /dev/null || err "ERROR: No encoding info in file"
    grep 'print("Hello World")' foo.py >/dev/null || err "ERROR: No black formatting perfomed"
    [[ "root" == *"$(stat -c \"%U\" foo.py)"* ]] && err "ERROR: File owned by root, not by current user."
    popd > /dev/null

# Install pre-commit toolkit as a pre-commit step in git. Run exactly once, overwritten by "pre-commit install"
install_hooks:
    #! /bin/bash
    cat <<EOF > .git/hooks/pre-commit
    docker run --rm -t -v $PWD:/src -v ~/.cache/pre-commit:/root/.cache/pre-commit {{docker_image}} 
    EOF
    chmod +x .git/hooks/pre-commit
